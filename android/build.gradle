// Copyright Â© 2018-2023 Kaleyra S.p.a. All Rights Reserved.
// See LICENSE for licensing information

buildscript {
  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:${catalog.versions.gradleVersion.get()}"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${catalog.versions.jetbrainsKotlinVersion.get()}"
  }
}

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

plugins {
  id 'com.google.devtools.ksp' version catalog.versions.googleDevToolsKspVersion
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply from: "../native-bridge/android/bridge.gradle"

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["ReactNativePlugin_" + name]).toInteger()
}

android {
  namespace "com.kaleyra.video_react_native_module"
  compileSdk getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
    buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()
  }
  buildTypes {
    release {
      minifyEnabled false
    }
  }

  buildFeatures {
    buildConfig true
  }

  sourceSets {
    main.java.srcDirs += '../native-bridge/android/src/main/java'
    test.java.srcDirs += '../native-bridge/android/src/test/java'
    test.resources.srcDirs += '../native-bridge/android/src/test/resources'
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }

  kotlinOptions {
    jvmTarget = '17'
  }

}

def REACT_NATIVE_VERSION = new File(['node', '--print', "JSON.parse(require('fs').readFileSync(require.resolve('react-native/package.json'), 'utf-8')).version"].execute(null, rootDir).text.trim())

dependencies {
  implementation "com.facebook.react:react-android:$REACT_NATIVE_VERSION"
}

if (isNewArchitectureEnabled()) {
  react {
    jsRootDir = file("../src/")
    libraryName = "VideoReactNativeModule"
    codegenJavaPackageName = "com.kaleyra.video_react_native_module"
  }
}
